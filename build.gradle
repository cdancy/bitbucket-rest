buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'org.jfrog.buildinfo:build-info-extractor-gradle:4.20.0'
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.4'
    }
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.artifactory'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'checkstyle'
apply plugin: 'jacoco'
apply plugin: 'eclipse'
apply plugin: 'pmd'

apply from: "$rootDir/gradle/additional-artifacts.gradle"
apply from: "$rootDir/gradle/dependencies.gradle"
apply from: "$rootDir/gradle/documentation.gradle"
apply from: "$rootDir/gradle/publishing.gradle"
apply from: "$rootDir/gradle/release.gradle"

repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    ext.jcloudsVersion = '2.2.1'
    ext.autoValueVersion = '1.7.5'
    ext.autoServiceVersion = '1.0-rc7'
    ext.guiceVersion = '4.2.3'

    compile ("org.apache.jclouds:jclouds-core:${jcloudsVersion}")
    compile ("com.google.inject:guice:${guiceVersion}")
    compile ("com.google.inject.extensions:guice-assistedinject:${guiceVersion}")
    compile ("com.google.auto.value:auto-value-annotations:${autoValueVersion}")
    annotationProcessor ("com.google.auto.value:auto-value:${autoValueVersion}")
    compile ("com.google.auto.service:auto-service-annotations:${autoServiceVersion}")
    annotationProcessor ("com.google.auto.service:auto-service:${autoServiceVersion}")

    compile ('javax.xml.bind:jaxb-api:2.3.1')

    testCompile ("org.apache.jclouds:jclouds-core:${jcloudsVersion}:tests")
    testCompile ("org.apache.jclouds.driver:jclouds-slf4j:${jcloudsVersion}")
    testCompile ('org.testng:testng:7.4.0')
    testCompile ('org.assertj:assertj-core:3.19.0')
    testCompile ('com.squareup.okhttp:mockwebserver:2.7.5')
    testCompile ('ch.qos.logback:logback-core:1.2.3')
    testCompile ('ch.qos.logback:logback-classic:1.2.3')
    testCompile ('commons-io:commons-io:2.8.0')
}

ext.compatibilityVersion = '1.8'
sourceCompatibility = compatibilityVersion
targetCompatibility = compatibilityVersion

jar {
    manifest {
        attributes 'Implementation-Title': 'Bitbucket REST client',
                   'Implementation-Version': version,
                   'Built-By': System.getProperty('user.name'),
                   'Built-Date': new Date(),
                   'Built-JDK': System.getProperty('java.version'),
                   'Built-Gradle': gradle.gradleVersion
    }
}

checkstyle {
    toolVersion = "6.13"
}

pmd {
    ruleSetConfig = resources.text.fromFile(rootProject.file('config/pmd/pmd.xml'))
    consoleOutput = true
    ignoreFailures = false
    ruleSets = []
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

tasks.withType(Pmd) {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

tasks.withType(JavaCompile) {
    options.compilerArgs += ['-Xlint:-options']
}

task mockTest(type: Test) {
    useTestNG()
    include '**/**MockTest.class'
    maxParallelForks = 2
    testLogging {
        showStandardStreams = true
        events 'started', 'passed', 'failed'
    }
}

task integTest(type: Test, dependsOn: ['mockTest']) {
    doFirst {
        def integProjectDir = project.file("${buildDir}/integ-projects")
        if (!integProjectDir.exists()) {
            if (!integProjectDir.mkdirs()) {
                throw new RuntimeException("Failed to create integ-project directory @ ${integProjectDir.path}")
            }
        }
    }
    useTestNG()
    include '**/**LiveTest.class'
    testLogging {
        showStandardStreams = true
        events 'started', 'passed', 'failed'
    }
    def authentication = [:]
    def possibleAuth = project.findProperty('testBitbucketRestCredentials')
    if (possibleAuth) {
        authentication['test.bitbucket.rest.credentials'] = possibleAuth
    } else {
        possibleAuth = project.findProperty('testBitbucketRestToken')
        if (possibleAuth) {
            authentication['test.bitbucket.rest.token'] = possibleAuth
        } else {
            logger.quiet 'No authentication parameters found. Assuming anonymous...'
        }
    }

    // property 'test.bitbucket.endpoint' needs to be
    // hard-coded in for jclouds test framework
    systemProperties = ["test.bitbucket.endpoint" : "${testBitbucketRestEndpoint}",
                        "test.bitbucket.project" : "${testBitbucketRestProject}",
                        "test.bitbucket.basedir" : "${buildDir}/integ-projects"] << authentication
}

